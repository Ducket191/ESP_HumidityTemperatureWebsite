#include <ESP8266WiFi.h>
#include <ESP8266HTTPClient.h>
#include <ArduinoJson.h>
#include "DHT.h"

#define DHTPIN 2    
#define DHTTYPE DHT22

DHT dht(DHTPIN, DHTTYPE);

const char* ssid = "IoT LAB";
const char* password = "kvt1ptit";
const char* serverUrl = "http://192.168.76.112:5000/api/data"; 

WiFiClient client;

void setup() {
  Serial.begin(115200);
  WiFi.begin(ssid, password);
  dht.begin();

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("\nWiFi connected");
  Serial.println("IP: " + WiFi.localIP().toString());
}

void loop() {
  if (WiFi.status() == WL_CONNECTED) {
    float temp = dht.readTemperature();
    float hum = dht.readHumidity();

    if (isnan(temp) || isnan(hum)) { 
      Serial.println("Failed to read from DHT sensor!");
      delay(2000);
      return;
    }

    HTTPClient http;
    http.begin(client, serverUrl);
    http.addHeader("Content-Type", "application/json");

    StaticJsonDocument<200> doc;
    doc["temperature"] = temp;
    doc["humidity"] = hum;
    String json;
    serializeJson(doc, json);

    int code = http.POST(json);
    Serial.println("HTTP Response: " + String(code));
    Serial.println("Temp: " + String(temp) + " Â°C");
    Serial.println("Hum: " + String(hum) + " %");

    if (code < 0) {
      Serial.println("Error: " + String(HTTPClient::errorToString(code)));
    }
    http.end();
  }
  delay(5000);
}
